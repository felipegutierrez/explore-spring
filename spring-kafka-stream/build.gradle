plugins {
	id 'java'
	id "com.github.davidmc24.gradle.plugin.avro" version "1.0.0"
	id 'jacoco'
	id 'idea' // optional (to generate IntelliJ IDEA project files)
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.github.edeandrea.xjc-generation' version '1.6'
}

description = "spring-kafka-stream"
group = 'com.github.felipegutierrez.explore.spring'
version = '0.0.1'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	gradlePluginPortal()
	maven { url "http://packages.confluent.io/maven/" }
	jcenter()
	maven { url "https://jitpack.io" }
}

ext {
	set('springCloudVersion', "Hoxton.SR10") // 2020.0.2 | Hoxton.SR10
	jaxbVersion = '2.3.1'
}

dependencies {
	implementation 'org.apache.kafka:kafka-streams'
	implementation 'org.springframework.cloud:spring-cloud-stream'
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.apache.avro:avro:1.10.2'
	implementation 'io.confluent:kafka-avro-serializer:6.1.1'
	implementation 'io.confluent:kafka-streams-avro-serde:6.1.1'
	implementation 'io.confluent:kafka-streams-json-schema-serde:6.1.1'
	implementation "javax.xml.bind:jaxb-api:$jaxbVersion"
	implementation "org.glassfish.jaxb:jaxb-runtime:$jaxbVersion"
	xjc "javax.xml.bind:jaxb-api:$jaxbVersion"
	xjc "org.glassfish.jaxb:jaxb-runtime:$jaxbVersion"
	xjc "org.glassfish.jaxb:jaxb-xjc:$jaxbVersion"
	xjc 'javax.activation:activation:1.1.1'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.apache.kafka:kafka-streams-test-utils'
	testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

sourceSets {
	test {
		java.srcDirs = ['src/test/java/unit', 'src/test/java/intg']
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

xjcGeneration {
	defaultAdditionalXjcOptions = ['encoding': 'UTF-8']
	schemas {
		order {
			schemaFile = 'order.xsd'
			javaPackageName = 'com.github.felipegutierrez.explore.spring.model'
		}
	}
}
